############################################################
# Libdragon entrypoint
#
# This is the entrypoint for all Libdragon applications. It
# is the first code run after the IPL3.
#
# Authors: Jennifer Taylor <dragonminded@dragonminded.com>
#          Giovanni Bajo <giovannibajo@gmail.com>
############################################################

#include "regs.S"

	.set noreorder

	.section .boot
	.global _start
_start:
	/* initialize small data pointer */
	la gp, _gp

	/* a bit from libgloss so we start at a known state */
	li v0,SR_CU1|SR_PE|SR_FR|SR_KX|SR_SX|SR_UX
	mtc0 v0,C0_SR

	/* fill .bss with 0s */
	la a0, __bss_start
	or a0, 0x20000000           /* convert address to KSEG1 (uncached) */
	la a1, __bss_end
	or a1, 0x20000000
.Lbss_init:
	sd $0,(a0)
	addiu a0,8
	bltu a0,a1, .Lbss_init
	nop

#if 0
	/* Run a check to verify that BSS is cleared. This is useful while debugging
	   IPL3 changes. */
	jal __bss_check
	nop
#endif

	/* Copy bootcode flags, stored in DMEM */
	lui v0, 0xA400
	lw t0, 0x0000(v0)
	lw t1, 0x0004(v0)
	lhu t2, 0x000C(v0)
	sw t0, %gprel(__boot_memsize)(gp)
	sw t1, %gprel(__entropy_state+0)(gp)
	sw t1, %gprel(__entropy_state+4)(gp)
	sw t2, %gprel(__boot_address_page)(gp)

	/* Copy other bootcode flags */
	lbu t0, 0x0009(v0)
	lbu t1, 0x000A(v0)
	lbu t2, 0x000B(v0)
	sw t0, %gprel(__boot_tvtype)(gp)
	sw t1, %gprel(__boot_resettype)(gp)
	sw t2, %gprel(__boot_consoletype)(gp)

	la t0, debug_assert_func    /* install assert function in system.c */
	la t1, __assert_func_ptr
	sw t0, 0(t1)	

	jal __do_global_ctors		/* call global constructors */
	nop
	li a0, 0
	jal main					/* call main app */
	li a1, 0

_abort:
	j _abort
	nop
